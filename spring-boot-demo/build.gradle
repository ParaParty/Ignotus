plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'java'
    id 'org.jetbrains.kotlin.jvm'
    id 'idea' // optional (to generate IntelliJ IDEA project files)
    id 'jacoco'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

def requiresModules = [
    'cache',
    'database-exposed',
    'exception',
    'foundation',
    'serializer',
    'support',
    'translation',
    'validation',
]

configurations.all {
    exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
}

dependencies {
    for (item in requiresModules) {
        api project(path: ":$item")
    }

    api 'org.springframework.boot:spring-boot-starter-web:2.6.1'
    api 'org.springframework.boot:spring-boot-starter-log4j2:2.6.1'
    api 'org.springframework.security:spring-security-crypto:5.6.0'
    api 'org.springframework.boot:spring-boot-starter-aop:2.6.1'
    testApi 'org.springframework.boot:spring-boot-starter-test:2.6.1'

    // Cache redis 支持
    api 'org.springframework.boot:spring-boot-starter-data-redis:2.6.1'
    api 'org.springframework.integration:spring-integration-redis:5.5.3'

    // Jackson 扩展
    api 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.13.0'
    api 'com.fasterxml.jackson.module:jackson-module-kotlin:2.13.0'

    api 'mysql:mysql-connector-java:8.0.27'

    // Translation yaml 文件支持
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.12.5'

    // Translation properties 文件支持
    api 'com.fasterxml.jackson.dataformat:jackson-dataformat-properties:2.12.5'

    // Translation kts 文件支持
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.0'
    runtimeOnly 'org.jetbrains.kotlin:kotlin-reflect:1.6.0'
    implementation 'org.jetbrains.kotlin:kotlin-script-runtime:1.6.0'
    implementation 'org.jetbrains.kotlin:kotlin-script-util:1.6.0'
    runtimeOnly 'org.jetbrains.kotlin:kotlin-compiler-embeddable:1.6.0'
    runtimeOnly 'org.jetbrains.kotlin:kotlin-scripting-compiler-embeddable:1.6.0'
}

test {
    useJUnitPlatform {
        // includeEngines 'junit-jupiter', 'junit-vintage'
        // excludeEngines 'custom-engine'

        // includeTags 'fast'
        excludeTags 'slow'
    }

    testLogging {
        events 'passed', 'skipped', 'failed'
    }

    finalizedBy(jacocoTestReport)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
